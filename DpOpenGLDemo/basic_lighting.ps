#version 330

const int MAX_POINT_LIGHTS = 2;
const int MAX_SPOT_LIGHTS = 2;

in vec2 TexCoord0;
in vec3 Normal0;
in vec3 WorldPos0;

out vec4 FragColor;

struct BaseLight
{
	vec3 color;
	float ambientIntensity;
	float diffuseIntensity;
};

struct DirectionalLight
{
	BaseLight base;
	vec3 direction;
};

struct Attenuation
{
	float kc;
	float kl;
	float kq;
};

struct PointLight
{
	BaseLight base;
	vec3 position;
	Attenuation atten;
};

struct SpotLight
{
	PointLight pl;
	vec3 direction;
	float cosThetaOver2;
	float cosPhiOver2;
	float pf;
};

uniform int gNumPointLights;
uniform int gNumSpotLights;
uniform sampler2D gSampler;
uniform vec3 gEyeWorldPos;
uniform float gSpecularIntensity;
uniform float gSpecularPower;
uniform DirectionalLight gDirectionalLight;
uniform PointLight gPointLights[MAX_POINT_LIGHTS];
uniform SpotLight gSpotLights[MAX_SPOT_LIGHTS];

vec4 CalcLightInternal(BaseLight light, vec3 lightDirection, vec3 normal)
{
	vec4 ambientColor = vec4(light.color * light.ambientIntensity, 1.0f);
	
	vec4 diffuseColor = vec4(0, 0, 0, 0);
	vec4 specularColor = vec4(0, 0, 0, 0);
	float k = dot(-lightDirection, normal);
	if (k > 0)
	{
		diffuseColor = vec4(light.color * light.diffuseIntensity * k, 1.0f);

		vec3 vertexToEye = normalize(gEyeWorldPos - WorldPos0);
		vec3 lightReflect = normalize(lightDirection - 2 * normal * dot(normal, lightDirection));
		k = dot(vertexToEye, lightReflect);
		if (k > 0)
		{
			k = pow(k, gSpecularPower);
			specularColor = vec4(light.color * gSpecularIntensity * k, 1.0f);
		}
	}

	return (ambientColor + diffuseColor + specularColor);
}

vec4 CalcDirectionLight(vec3 normal)
{
	return CalcLightInternal(gDirectionalLight.base, gDirectionalLight.direction, normal);
}

vec4 CalcPointLight(PointLight pl, vec3 normal)
{
	vec3 lightDirection = WorldPos0 - pl.position;
	float distance = length(lightDirection);
	lightDirection = normalize(lightDirection);

	vec4 color = CalcLightInternal(pl.base, lightDirection, normal);
	float k = pl.atten.kc + pl.atten.kl * distance + pl.atten.kq * distance * distance;

	return color / k;
}

vec4 CalcSpotLight(SpotLight sl, vec3 normal)
{
	vec3 lightToPixel = normalize(WorldPos0 - sl.pl.position);

	vec4 color = vec4(0, 0, 0, 0);
	float k1 = dot(lightToPixel, sl.direction);

	// case 1: alpha > phi / 2, no ligthing
	if (k1 < sl.cosPhiOver2)
	{
		return color;
	}
	// case 2: alpha < theta, as point light
	else if(k1 > sl.cosThetaOver2)
	{
		color = CalcPointLight(sl.pl, normal);
	}
	// case 3: theta / 2 < alpha < phi / 2
	else
	{
		color = CalcPointLight(sl.pl, normal);
		float k2 = pow(k1 - sl.cosPhiOver2, sl.pf);
		float k3 = sl.cosThetaOver2 - sl.cosPhiOver2;
		color = color * k2 / k3;
	}

	return color;
}

void main()
{
	vec3 normal = normalize(Normal0);
	vec4 totalLight = CalcDirectionLight(normal);

	for(int i = 0; i < gNumPointLights; ++i)
	{
		totalLight += CalcPointLight(gPointLights[i], normal);
	}

	for(int i = 0; i < gNumSpotLights; ++i)
	{
		totalLight += CalcSpotLight(gSpotLights[i], normal);
	}

	FragColor = texture2D(gSampler, TexCoord0.xy) * totalLight;
}